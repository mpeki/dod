
plugins {
	id 'jacoco' // core plugin â€“ no alias needed
	alias(libs.plugins.springboot)                 apply false
	alias(libs.plugins.sonarqube)
	alias(libs.plugins.dep.mgmt)                   apply false
	alias(libs.plugins.jib)                        apply false
	alias(libs.plugins.git.props)
	alias(libs.plugins.frontend)                   apply false
	alias(libs.plugins.asciidoctor)                apply false
	alias(libs.plugins.docker.compose)             apply false
	alias(libs.plugins.rewrite)                    apply false
}

group = 'dk.dodgame'

ext {
	set('snippetsDir', file("build/generated-snippets"))
}

allprojects {
	repositories {
		mavenLocal()
		mavenCentral()
		maven {
			name = "GitHubPackages"
			url = uri("https://maven.pkg.github.com/mpeki/droolsassert")
			credentials {
				username = "${System.env.USERNAME}"
				password = "${System.env.GITHUB_TOKEN}"
			}
		}
	}
}
subprojects {
	if (project != project(':frontend') &&
			project != project(':selenium-tests') &&
			project != project(':security') &&
			project != project(':security:keycloak') &&
			project != project(':security:keycloak:theme')) {
		apply from: "$rootDir/gradle/test.gradle"
		apply from: "$rootDir/gradle/liquibase.gradle"
		apply from: "$rootDir/gradle/publish.gradle"
	}

	ext {
		mavenBom = org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
	}
}

gitProperties {
	extProperty = "gitProps" // git properties will be put in a map at project.ext.gitProps
}
generateGitProperties.outputs.upToDateWhen { false }

sonarqube {
	properties {
		property "sonar.gradle.skipCompile", "true"
		property "sonar.projectKey", "mpeki_dod"
		property "sonar.organization", "mpeki"
		property "sonar.host.url", "https://sonarcloud.io"
		property "sonar.token", System.getenv('SONAR_TOKEN') // This should be an environment variable
	}
}