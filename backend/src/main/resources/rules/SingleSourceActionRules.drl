import dk.pekilidi.dod.data.CharacterDTO
import dk.pekilidi.dod.character.model.CharacterState
import dk.pekilidi.dod.util.Dice
import dk.pekilidi.dod.actions.model.SkillTrainingAction
import dk.pekilidi.dod.actions.model.Type
import dk.pekilidi.dod.actions.model.ActionResult
import dk.pekilidi.dod.skill.SkillService
import dk.pekilidi.dod.skill.SkillNotFoundException
import dk.pekilidi.dod.data.SkillDTO
import dk.pekilidi.dod.changerequest.model.ChangeStatus
import dk.pekilidi.dod.util.rules.RulesUtil
import dk.pekilidi.dod.character.model.BaseTraitName
import java.time.LocalDateTime
import dk.pekilidi.dod.skill.model.Category

rule "Validate Action - Character is not ready to play"
  ruleflow-group "training-action"
  salience 100
  when
    ssAction : SkillTrainingAction( type == Type.SKILL_TRAINING && ssAction.actionResult == null );
    character : CharacterDTO( state != CharacterState.READY_TO_PLAY ) from ssAction.actor;
  then
    ssAction.setActionResult(ActionResult.INVALID_ACTION);
    ssAction.setResultDescription("Character is not ready to play - must be in READY_TO_PLAY state, was " + character.getState());
    update(ssAction);
end;

rule "Validate Action - Skill does not exist!"
  ruleflow-group "training-action"
  salience 100
  when
    ssAction : SkillTrainingAction( type == Type.SKILL_TRAINING && ssAction.skill == null && ssAction.actionResult == null );
    character : CharacterDTO( state == CharacterState.READY_TO_PLAY ) from ssAction.actor;
  then
    ssAction.setActionResult(ActionResult.INVALID_ACTION);
    ssAction.setResultDescription("Skill does not exist: " + ssAction.getSkillKey() + " how did you do that?");
    update(ssAction);
end;

rule "Get result from training action (single actor no target)"
  ruleflow-group "training-action"
  salience 90
  when
    ssAction : SkillTrainingAction( type == Type.SKILL_TRAINING && ssAction.actionResult == null );
    character : CharacterDTO( state == CharacterState.READY_TO_PLAY ) from ssAction.actor;
    roll : Integer() from Dice.roll("1t20");
  then
    String skillKey = ssAction.getSkillKey();
    SkillDTO characterSkill = character.getSkills().get(skillKey);

    if(characterSkill != null){
      int fv = characterSkill.getFv();
      characterSkill.setCategory(ssAction.getSkill().getCategory());
      characterSkill.setLastUsed(LocalDateTime.now());
      ssAction.setActionResult(RulesUtil.testSkill(characterSkill, roll, ssAction.getDifficulty()));
      ssAction.setResultDescription("Rolled ["+roll+"] when testing known skill: " + skillKey + " - with fv: " + fv );
    } else {
        if(ssAction.getSkill().getBaseChance() == BaseTraitName.NONE) {
          ssAction.setActionResult(RulesUtil.testSkill(-1, roll, ssAction.getDifficulty()));
          ssAction.setResultDescription("Rolled ["+roll+"] when testing skill unknown to character (no base chance): " + skillKey );
        } else {
          int baseChance = character.getBaseTraits().get(ssAction.getSkill().getBaseChance()).getGroupValue();
          ssAction.setActionResult(RulesUtil.testSkill(baseChance, roll, ssAction.getDifficulty()));
          ssAction.setResultDescription("Rolled ["+roll+"] when testing skill unknown to character (with base chance): " + skillKey + " - with fv: " + baseChance );
        }
    }
    update(ssAction);
end;
