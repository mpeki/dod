import dk.pekilidi.dod.data.CharacterTemplateDTO
import dk.pekilidi.dod.data.CharacterDTO
import java.util.Arrays;
import dk.pekilidi.dod.util.Dice
import dk.pekilidi.dod.data.BaseTraitRuleDTO
import dk.pekilidi.dod.data.BaseTraitDTO
import java.util.List
import java.util.Collections
import java.util.ArrayList
import dk.pekilidi.dod.character.model.CharacterState;
import dk.pekilidi.dod.character.model.AgeGroup
import dk.pekilidi.dod.character.model.BaseTraitName;
import dk.pekilidi.dod.changerequest.model.ChangeType
import dk.pekilidi.dod.changerequest.model.ChangeRequest
import dk.pekilidi.dod.changerequest.model.ChangeStatus
import dk.pekilidi.dod.changerequest.model.ChangeStatusLabel
import dk.pekilidi.dod.character.model.BaseTraitName
import dk.pekilidi.dod.changerequest.model.ChangeStatusLabel
import dk.pekilidi.dod.changerequest.model.ChangeStatus
import dk.pekilidi.dod.changerequest.model.ChangeStatusLabel
import java.sql.SQLOutput
import dk.pekilidi.dod.validation.RegularExpressions
import dk.pekilidi.dod.character.model.Looks

rule "Character change - Change type not implemented"
  no-loop
  ruleflow-group "character-change"
  when
    changeRequest : ChangeRequest( changeType == ChangeType.BASE_SKILL_POINTS )
  then
    changeRequest.setStatusLabel(ChangeStatusLabel.NOT_IMPLEMENTED);
    changeRequest.setStatus(ChangeStatus.REJECTED);
end;

rule "Character change - must be hero"
  no-loop
  ruleflow-group "character-change"
  when
    character : CharacterDTO( hero == false )
    changeRequest : ChangeRequest( changeType == ChangeType.BASE_TRAIT || changeType == ChangeType.HERO_POINTS || changeType == ChangeType.HIT_POINTS)
  then
    changeRequest.setStatusLabel(ChangeStatusLabel.NO_HERO);
    changeRequest.setStatus(ChangeStatus.REJECTED);
end;


rule "Character change - check hero points"
  no-loop
  ruleflow-group "character-change"
  when
    changeRequest : ChangeRequest( changeType == ChangeType.BASE_TRAIT )
    change : Integer() from changeRequest.getModifier
    cost : Number() from change * 2
    character : CharacterDTO( state == CharacterState.INIT_COMPLETE && heroPoints - cost.intValue() < 0)
  then
    changeRequest.setStatusLabel(ChangeStatusLabel.INSUFFICIENT_HERO_POINTS);
    changeRequest.setStatus(ChangeStatus.REJECTED);
end;

rule "Character change - modify base trait"
  no-loop
  ruleflow-group "character-change"
  when
    changeRequest : ChangeRequest( changeType == ChangeType.BASE_TRAIT )
    change : Integer() from changeRequest.getModifier
    cost : Number() from change * 2
    character : CharacterDTO( state == CharacterState.INIT_COMPLETE && heroPoints - cost.intValue() >= 0)
  then
    changeRequest.setStatus(ChangeStatus.APPROVED);
    changeRequest.setStatusLabel(ChangeStatusLabel.OK_BASE_TRAIT_MODIFICATION);
    character.setHeroPoints(character.getHeroPoints() - cost.intValue());
    BaseTraitName baseTraitName = (BaseTraitName) changeRequest.getChangeKey();
    character.incrementTrait(baseTraitName, change);
end;

rule "Character change - modify hero points"
  no-loop
  ruleflow-group "character-change"
  when
    character : CharacterDTO( state == CharacterState.INIT_COMPLETE && hero == true)
    changeRequest : ChangeRequest( changeType == ChangeType.HERO_POINTS )
    change : Integer() from changeRequest.getModifier
  then
    changeRequest.setStatus(ChangeStatus.APPROVED);
    changeRequest.setStatusLabel(ChangeStatusLabel.OK_HERO_POINTS_INCREASE);
    character.setHeroPoints(character.getHeroPoints() + change);
end;

rule "Character change - make character ready for play - wrong state"
  no-loop
  ruleflow-group "character-state-change"
  when
    changeRequest : ChangeRequest( changeType == ChangeType.CHARACTER_READY_TO_PLAY )
    character : CharacterDTO( state != CharacterState.INIT_COMPLETE )
  then
    changeRequest.setStatus(ChangeStatus.REJECTED);
    changeRequest.setStatusLabel(ChangeStatusLabel.WRONG_CHARACTER_STATE);
end;

rule "Character change - make character ready for play - to many BSPs"
  no-loop
  ruleflow-group "character-state-change"
  when
    changeRequest : ChangeRequest( changeType == ChangeType.CHARACTER_READY_TO_PLAY )
    character : CharacterDTO( state == CharacterState.INIT_COMPLETE && baseSkillPoints >= 10)
  then
    changeRequest.setStatus(ChangeStatus.REJECTED);
    changeRequest.setStatusLabel(ChangeStatusLabel.TOO_MANY_SKILLS_POINTS);
end;

rule "Character change - make character ready for play"
  no-loop
  ruleflow-group "character-state-change"
  when
    changeRequest : ChangeRequest( changeType == ChangeType.CHARACTER_READY_TO_PLAY )
    character : CharacterDTO( state == CharacterState.INIT_COMPLETE && baseSkillPoints < 10)
  then
    changeRequest.setStatus(ChangeStatus.APPROVED);
    changeRequest.setStatusLabel(ChangeStatusLabel.OK_READY_FOR_PLAY);
    character.setState(CharacterState.READY_TO_PLAY);
end;

rule "Character change - init complete changes"
  no-loop
  ruleflow-group "character-change"
  when
    changeRequest : ChangeRequest( changeType == ChangeType.CHARACTER_CHANGE_NAME ||
                                    changeType == ChangeType.CHARACTER_CHANGE_HEIGHT ||
                                    changeType == ChangeType.CHARACTER_CHANGE_WEIGHT)
    character : CharacterDTO( state != CharacterState.INIT_COMPLETE )
  then
    changeRequest.setStatus(ChangeStatus.REJECTED);
    changeRequest.setStatusLabel(ChangeStatusLabel.WRONG_CHARACTER_STATE);
end;


rule "Character name change - validate new Name"
  no-loop
  ruleflow-group "character-change"
  when
    changeRequest : ChangeRequest( changeType == ChangeType.CHARACTER_CHANGE_NAME )
    ChangeRequest( modifier not matches RegularExpressions.VALID_CHARACTER_NAME )
  then
    changeRequest.setStatus(ChangeStatus.REJECTED);
    changeRequest.setStatusLabel(ChangeStatusLabel.INVALID_NAME);
end;

rule "Character name change - validate new height"
  no-loop
  ruleflow-group "character-change"
  when
    changeRequest : ChangeRequest( changeType == ChangeType.CHARACTER_CHANGE_HEIGHT )
    ChangeRequest( modifier not matches RegularExpressions.VALID_CHARACTER_HEIGHT )
  then
    changeRequest.setStatus(ChangeStatus.REJECTED);
    changeRequest.setStatusLabel(ChangeStatusLabel.INVALID_HEIGHT);
end;

rule "Character name change - validate new weight"
  no-loop
  ruleflow-group "character-change"
  when
    changeRequest : ChangeRequest( changeType == ChangeType.CHARACTER_CHANGE_WEIGHT )
    ChangeRequest( modifier not matches RegularExpressions.VALID_CHARACTER_WEIGHT )
  then
    changeRequest.setStatus(ChangeStatus.REJECTED);
    changeRequest.setStatusLabel(ChangeStatusLabel.INVALID_WEIGHT);
end;


rule "Character name change - ok"
  no-loop
  ruleflow-group "character-change"
  when
    ChangeRequest( modifier matches RegularExpressions.VALID_CHARACTER_NAME )
    changeRequest : ChangeRequest( changeType == ChangeType.CHARACTER_CHANGE_NAME )
    character : CharacterDTO( state == CharacterState.INIT_COMPLETE )
  then
    if(changeRequest.getModifier() instanceof String){
      character.setName((String)changeRequest.getModifier());
      changeRequest.setStatus(ChangeStatus.APPROVED);
      changeRequest.setStatusLabel(ChangeStatusLabel.OK_NAME);
    } else {
      changeRequest.setStatus(ChangeStatus.REJECTED);
      changeRequest.setStatusLabel(ChangeStatusLabel.INVALID_NAME);
    }
end;

rule "Character height change - ok"
  no-loop
  ruleflow-group "character-change"
  when
    ChangeRequest( modifier matches RegularExpressions.VALID_CHARACTER_HEIGHT )
    changeRequest : ChangeRequest( changeType == ChangeType.CHARACTER_CHANGE_HEIGHT )
    character : CharacterDTO( state == CharacterState.INIT_COMPLETE )
  then
    if(changeRequest.getModifier() instanceof String){
      character.getLooks().setHeight(Integer.parseInt((String)changeRequest.getModifier()));
      changeRequest.setStatus(ChangeStatus.APPROVED);
      changeRequest.setStatusLabel(ChangeStatusLabel.OK_HEIGHT);
    } else {
      changeRequest.setStatus(ChangeStatus.REJECTED);
      changeRequest.setStatusLabel(ChangeStatusLabel.INVALID_HEIGHT);
    }
end;

rule "Character weight change - ok"
  no-loop
  ruleflow-group "character-change"
  when
    ChangeRequest( modifier matches RegularExpressions.VALID_CHARACTER_WEIGHT )
    changeRequest : ChangeRequest( changeType == ChangeType.CHARACTER_CHANGE_WEIGHT )
    character : CharacterDTO( state == CharacterState.INIT_COMPLETE )
  then
    if(changeRequest.getModifier() instanceof String){
      character.getLooks().setWeight(Integer.parseInt((String)changeRequest.getModifier()));
      changeRequest.setStatus(ChangeStatus.APPROVED);
      changeRequest.setStatusLabel(ChangeStatusLabel.OK_WEIGHT);
    } else {
      changeRequest.setStatus(ChangeStatus.REJECTED);
      changeRequest.setStatusLabel(ChangeStatusLabel.INVALID_WEIGHT);
    }
end;
